apply plugin: "groovy"
apply plugin: "idea"

repositories {
    mavenCentral()
}

configurations {
    // wanted jmeter selenium dependencies isolated
    jmeterDep {
        transitive = true
    }
    compile {
        //set compile configuration to extend from jmeterDep
        extendsFrom jmeterDep
    }
}
ext.seleniumVersion = "2.37.1"
ext.phantomjsdriverVersion = "1.0.4"
ext.jmeterVersion = "2.10"

dependencies {
    jmeterDep "org.codehaus.groovy:groovy-all:2.1.6"
    jmeterDep "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
    jmeterDep "com.github.detro.ghostdriver:phantomjsdriver:$phantomjsdriverVersion"
    jmeterDep "org.apache.commons:commons-lang3:3.1"


    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "junit:junit:4.11"
}

test {
    systemProperties = System.getProperties()
    outputs.upToDateWhen { false }
}
// adding tests to jar
jar {
    from sourceSets.test.output
}
task jmeterDependencyDeploy(type: Copy) {
    from(configurations.jmeterDep)
    into("apache-jmeter-${jmeterVersion}/lib")
}
task jmeterDeploy(type: Copy, dependsOn: ["jar", "jmeterDependencyDeploy"]) {
    from("build/libs")
    into("apache-jmeter-${jmeterVersion}/lib/junit")
    include("*.jar")
}


task jmeterInstall << {
    // optionally download JMeter and install
    final String jmeterName = "apache-jmeter-${jmeterVersion}"
    final File jmetertgz = new File(buildDir, "${jmeterName}.tgz")
    if (!jmetertgz.exists()) {
        GFileUtils.copyURLToFile(new URL("http://apache.mirrors.tds.net//jmeter/binaries/${jmeterName}.tgz"), jmetertgz)
    }
    copy {
        from tarTree(jmetertgz)
        into getProjectDir()
    }
    // optionally download JMeter Standard Plugin and install
    final File jmeterPluginsStdZip = new File(buildDir, "JMeterPlugins-Standard-1.1.2.zip")
    if (!jmeterPluginsStdZip.exists()) {
        GFileUtils.copyURLToFile(new URL("http://jmeter-plugins.org/downloads/file/JMeterPlugins-Standard-1.1.2.zip"), jmeterPluginsStdZip)
    }
    copy {
        from zipTree(jmeterPluginsStdZip)
        into(new File(getProjectDir(), jmeterName))
    }
    // optionally download ServerAgent that supports a component in Standard Plugin to monitor servers during tests
    final String serverAgentName = "ServerAgent-2.2.1"
    final File serverAgentZip = new File(buildDir, "${serverAgentName}.zip")
    if (!new File(buildDir, "ServerAgent-2.2.1.zip").exists()) {
        GFileUtils.copyURLToFile(new URL("http://jmeter-plugins.org/downloads/file/ServerAgent-2.2.1.zip"), serverAgentZip)
    }
    copy {
        from zipTree(serverAgentZip)
        into(new File(getProjectDir(), "${jmeterName}/${serverAgentName}"))
    }
}

task createDirs << {
    sourceSets*.allSource.srcDirs*.each { dir ->
        ant.touch(file: new File(dir, ".gitignore"), mkdirs: true)
    }
    sourceSets*.resources.srcDirs*.each { dir ->
        ant.touch(file: new File(dir, ".gitignore"), mkdirs: true)
    }
}
task wrapper(type: Wrapper) {
    gradleVersion = "1.8"
}

task(console, dependsOn: "classes", type: JavaExec) {
    main = "groovy.ui.Console"
    classpath = sourceSets.main.runtimeClasspath
}
